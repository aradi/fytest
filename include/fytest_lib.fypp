#! Renders the test backend library
#!
#:def fytest_render_test_library()
module fytest_lib
  implicit none

  type :: fytest_TestStatusHelper
    integer :: testNotRun = -1
    integer :: testSucceded = 0
    integer :: testSkipped = 1
    integer :: testFailed = 2
  end type fytest_TestStatusHelper

  type(fytest_TestStatusHelper), parameter :: fytest_TestStatus = fytest_TestStatusHelper()


  type :: fytest_TestResult
    integer :: testStatus
    character(:), allocatable :: errorMsg
    integer :: line
  end type fytest_TestResult


  abstract interface
    subroutine fytest_TestRunnerIface(testName, testParams)
      import :: fytest_TestResult
      character(*), intent(in) :: testName
      integer, intent(in) :: testParams(:)
      !type(fytest_TestResult), intent(out) :: testResult
    end subroutine fytest_TestRunnerIface
  end interface


  type :: fytest_VarChar
    character(:), allocatable :: str
  end type fytest_VarChar


  type :: fytest_FixtureParams
    character(:), allocatable :: name
    type(fytest_VarChar), allocatable :: tests(:)
    integer, allocatable :: paramBounds(:,:)
    procedure(fytest_TestRunnerIface), pointer, nopass :: testRunner => null()
  end type fytest_FixtureParams


  type :: fytest_SuiteParams
    character(:), allocatable :: name
    character(:), allocatable :: fileName
    type(fytest_FixtureParams), allocatable :: fixtures(:)
  end type fytest_SuiteParams


  type :: fytest_TestContainer
    character(:), allocatable :: fileName, suiteName, fixtureName, testName
    integer, allocatable :: testParams(:)
    procedure(fytest_TestRunnerIface), pointer, nopass :: testRunner => null()
  end type fytest_TestContainer


contains

  subroutine fytest_TestContainer_initialize(this, suite, fixture, iTest, iParamTest)
    type(fytest_TestContainer), intent(out) :: this
    type(fytest_SuiteParams), intent(in) :: suite
    type(fytest_FixtureParams), intent(in) :: fixture
    integer, intent(in) :: iTest
    integer, intent(in) :: iParamTest

    this%fileName = suite%fileName
    this%suiteName = suite%name
    this%fixtureName = fixture%name
    this%testName = fixture%tests(iTest)%str
    this%testRunner => fixture%testRunner
    if (iParamTest > 0) then
      this%testParams = fytest_getParams(iParamTest, fixture%paramBounds)
    else
      this%testParams = [integer ::]
    end if

  end subroutine fytest_TestContainer_initialize


  subroutine fytest_getFlatTestArray(suites, testContainers)
    type(fytest_SuiteParams), intent(in) :: suites(:)
    type(fytest_TestContainer), allocatable, intent(out) :: testContainers(:)

    integer :: nSuites, iSuite, iFixture, iTest, nParamTests, iParam, ind
    integer, allocatable :: params(:)
    integer :: nAllTests

    nAllTests = 0
    do iSuite = 1, size(suites)
      associate (suite => suites(iSuite))
        do iFixture = 1, size(suite%fixtures)
          associate (fixture => suite%fixtures(iFixture))
            do iTest = 1, size(fixture%tests)
              if (size(fixture%paramBounds) > 0) then
                nParamTests = product(fixture%paramBounds(2, :) - fixture%paramBounds(1, :) + 1)
              else
                nParamTests = 1
              end if
              nAllTests = nAllTests + nParamTests
            end do
          end associate
        end do
      end associate
    end do

    allocate(testContainers(nAllTests))

    ind = 1
    do iSuite = 1, size(suites)
      associate (suite => suites(iSuite))
        do iFixture = 1, size(suite%fixtures)
          associate (fixture => suite%fixtures(iFixture))
            do iTest = 1, size(fixture%tests)
              if (size(fixture%paramBounds) > 0) then
                nParamTests = product(fixture%paramBounds(2, :) - fixture%paramBounds(1, :) + 1)
                do iParam = 1, nParamTests
                  call fytest_TestContainer_initialize(testContainers(ind), suite, fixture,&
                      & iTest, iParam)
                  ind = ind + 1
                end do
              else
                call fytest_TestContainer_initialize(testContainers(ind), suite, fixture, iTest, 0)
                ind = ind + 1
              end if
            end do
          end associate
        end do
      end associate
    end do

  end subroutine fytest_getFlatTestArray


  subroutine fytest_runTests(testContainers)
    type(fytest_TestContainer), intent(in) :: testContainers(:)

    integer :: iTest

    do iTest = 1, size(testContainers)
      associate (test => testContainers(iTest))
        call test%testRunner(test%testName, test%testParams)
      end associate
    end do

  end subroutine fytest_runTests


  function fytest_getParams(ind, paramBounds) result(params)
    integer, intent(in) :: ind
    integer, intent(in) :: paramBounds(:,:)
    integer :: params(size(paramBounds, dim=2))

    integer :: nParams(size(paramBounds, dim=2))
    integer :: ind0, divisor, iParam

    nParams(:) = paramBounds(2, :) - paramBounds(1, :) + 1
    ind0 = ind - 1
    divisor = product(nParams)
    do iParam = size(paramBounds, dim=2), 2, -1
      divisor = divisor / nParams(iParam)
      params(iParam) = ind0 / divisor
      ind0 = ind0 - divisor * params(iParam)
    end do
    params(1) = ind0
    params(:) = params + 1

  end function fytest_getParams


end module fytest_lib
#:enddef fytest_render_test_library
